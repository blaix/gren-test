module Test.Internal exposing (Test(..), blankDescriptionFailure, duplicatedName, failNow, toString)

import Random
import Set exposing (Set)
import Test.Expectation exposing (Expectation)
import Test.Runner.Failure exposing (InvalidReason(..), Reason(..))


{-| All variants of this type has the `GrenTestVariant__` prefix so that
node-test-runner can recognize them in the compiled JavaScript. This lets us
add more variants here without having to update the runner.

For more information, see <https://github.com/elm-explorations/test/pull/153>

-}
type Test
    = GrenTestVariant__UnitTest ({} -> Array Expectation)
    | GrenTestVariant__FuzzTest (Random.Seed -> Int -> Array Expectation)
    | GrenTestVariant__Labeled String Test
    | GrenTestVariant__Skipped Test
    | GrenTestVariant__Only Test
    | GrenTestVariant__Batch (Array Test)


{-| Create a test that always fails for the given reason and description.
-}
failNow : { description : String, reason : Reason } -> Test
failNow record =
    GrenTestVariant__UnitTest
        (\{} -> [ Test.Expectation.fail record ])


blankDescriptionFailure : Test
blankDescriptionFailure =
    failNow
        { description = "This test has a blank description. Let's give it a useful one!"
        , reason = Invalid BadDescription
        }


duplicatedName : Array Test -> Result (Set String) (Set String)
duplicatedName tests =
    let
        names : Test -> Array String
        names test =
            case test of
                GrenTestVariant__Labeled str _ ->
                    [ str ]

                GrenTestVariant__Batch subtests ->
                    Array.flatMap names subtests

                GrenTestVariant__UnitTest _ ->
                    []

                GrenTestVariant__FuzzTest _ ->
                    []

                GrenTestVariant__Skipped subTest ->
                    names subTest

                GrenTestVariant__Only subTest ->
                    names subTest

        accumDuplicates : String -> { dups : Set String, uniques : Set String } -> { dups : Set String, uniques : Set String }
        accumDuplicates newName { dups, uniques } =
            if Set.member newName uniques then
                { dups = Set.set newName dups, uniques = uniques }

            else
                { dups = dups, uniques = Set.set newName uniques }

        { dups = dupsAccum, uniques = uniquesAccum } =
            Array.flatMap names tests
                |> Array.foldl accumDuplicates { dups = Set.empty, uniques = Set.empty }
    in
    if Set.isEmpty dupsAccum then
        Ok uniquesAccum

    else
        Err dupsAccum


toString : a -> String
toString =
    Gren.Kernel.Debug.toString
