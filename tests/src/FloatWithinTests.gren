module FloatWithinTests exposing (floatWithinTests)

import Expect exposing (FloatingPointTolerance(..))
import Fuzz exposing (..)
import Helpers exposing (..)
import Test exposing (..)
import Math


floatWithinTests : Test
floatWithinTests =
    describe "Expect.within"
        [ describe "use-cases"
            [ fuzz niceFloat "pythagorean identity" <|
                \x ->
                    Math.sin x ^ 2 + Math.cos x ^ 2 |> Expect.within (AbsoluteOrRelative 0.000001 0.00001) 1.0
            , test "floats known to not add exactly" <|
                \_ -> 0.1 + 0.2 |> Expect.within (Absolute 0.000000001) 0.3
            , test "approximation of pi" <|
                \_ -> 3.14 |> Expect.within (Absolute 0.01) Math.pi
            , fuzz (floatRange 0.000001 100000) "relative tolerance of circle circumference using pi approximation" <|
                \radius ->
                    (radius * Math.pi)
                        |> Expect.within (Relative 0.001) (radius * 3.14)
            , test "approximation of pi is not considered too accurate" <|
                \_ ->
                    3.14
                        |> Expect.within (Absolute 0.001) Math.pi
                        |> expectToFail
            , test "too high absolute tolerance of circle circumference using pi approximation" <|
                \_ ->
                    expectTestToFail <|
                        fuzz (floatRange 0.000001 100000) "x" <|
                            \radius ->
                                (radius * Math.pi)
                                    |> Expect.within (Absolute 0.001) (radius * 3.14)
            , test "too high relative tolerance of circle circumference using pi approximation" <|
                \_ ->
                    expectTestToFail <|
                        fuzz (floatRange 0.000001 100000) "x" <|
                            \radius ->
                                (radius * Math.pi)
                                    |> Expect.within (Relative 0.0001) (radius * 3.14)
            ]
        , describe "use-cases with negative nominal and/or actual values"
            [ test "negative nominal and actual with Absolute" <|
                \_ -> -2.9 |> Expect.within (Absolute 0.1) -3
            , test "negative nominal and actual with Relative" <|
                \_ -> -2.9 |> Expect.within (Relative 0.1) -3
            , test "negative nominal and actual with AbsoluteOrRelative and pass on Absolute" <|
                \_ -> -2.9 |> Expect.within (AbsoluteOrRelative 0.1 0.0001) -3
            , test "negative nominal and actual with AbsoluteOrRelative and pass on Relative" <|
                \_ -> -2.9 |> Expect.within (AbsoluteOrRelative 0.001 0.05) -3
            , test "negative nominal and positive actual with Absolute" <|
                \_ -> 0.001 |> Expect.within (Absolute 3.3) -3
            , test "negative nominal and positive actual with Relative" <|
                \_ -> 0.001 |> Expect.within (Relative 1.1) -3
            , test "negative actual and positive nominal with Absolute" <|
                \_ -> -0.001 |> Expect.within (Absolute 3.3) 3
            , test "negative actual and positive nominal with Relative" <|
                \_ -> -0.001 |> Expect.within (Relative 1.1) 3
            , test "negative nominal should fail as actual is close, but positive with Absolute" <|
                \_ ->
                    2.9
                        |> Expect.within (Absolute 0.1) -3
                        |> expectToFail
            , test "negative nominal should fail as actual is close, but positive with Relative" <|
                \_ ->
                    2.9
                        |> Expect.within (Relative 0.1) -3
                        |> expectToFail
            ]
        , describe "edge-cases"
            [ fuzz2 niceFloat niceFloat "self equality" <|
                \epsilon value ->
                    let
                        eps =
                            if epsilon /= 0 then
                                epsilon

                            else
                                1
                    in
                    value |> Expect.within (Relative (Math.abs eps)) value
            , fuzz niceFloat "NaN inequality" <|
                \epsilon ->
                    let
                        nan =
                            0.0 / 0.0
                    in
                    nan |> Expect.notWithin (Relative (Math.abs epsilon)) nan
            , fuzz2 niceFloat niceFloat "NaN does not equal anything" <|
                \epsilon a ->
                    let
                        nan =
                            0.0 / 0.0
                    in
                    nan |> Expect.notWithin (Relative (Math.abs epsilon)) a
            , fuzz niceFloat "Infinity equality" <|
                \epsilon ->
                    let
                        infinity =
                            1.0 / 0.0
                    in
                    infinity |> Expect.within (Relative (Math.abs epsilon)) infinity
            , fuzz niceFloat "Negative infinity equality" <|
                \epsilon ->
                    let
                        negativeInfinity =
                            -1.0 / 0.0
                    in
                    negativeInfinity |> Expect.within (Relative (Math.abs epsilon)) negativeInfinity
            , fuzz3 niceFloat niceFloat niceFloat "within and notWithin should never agree on relative tolerance" <|
                \epsilon a b ->
                    let
                        withinTest =
                            a |> Expect.within (Relative (Math.abs epsilon)) b

                        notWithinTest =
                            a |> Expect.notWithin (Relative (Math.abs epsilon)) b
                    in
                    different withinTest notWithinTest
            , fuzz3 niceFloat niceFloat niceFloat "within and notWithin should never agree on absolute tolerance" <|
                \epsilon a b ->
                    let
                        withinTest =
                            a |> Expect.within (Absolute (Math.abs epsilon)) b

                        notWithinTest =
                            a |> Expect.notWithin (Absolute (Math.abs epsilon)) b
                    in
                    different withinTest notWithinTest
            , fuzz2 (pair niceFloat niceFloat) (pair niceFloat niceFloat) "within and notWithin should never agree on absolute or relative tolerance" <|
                \{ first = absoluteEpsilon, second = relativeEpsilon } { first = a, second = b } ->
                    let
                        withinTest =
                            a |> Expect.within (AbsoluteOrRelative (Math.abs absoluteEpsilon) (Math.abs relativeEpsilon)) b

                        notWithinTest =
                            a |> Expect.notWithin (AbsoluteOrRelative (Math.abs absoluteEpsilon) (Math.abs relativeEpsilon)) b
                    in
                    different withinTest notWithinTest
            , fuzz niceFloat "Zero equality" <|
                \epsilon -> 0.0 |> Expect.within (Relative (Math.abs epsilon)) 0.0
            , fuzz3 niceFloat niceFloat niceFloat "within absolute commutativity" <|
                \epsilon a b ->
                    same (Expect.within (Absolute (Math.abs epsilon)) a b) (Expect.within (Absolute (Math.abs epsilon)) b a)
            , fuzz3 niceFloat niceFloat niceFloat "notWithin absolute commutativity" <|
                \epsilon a b ->
                    same (Expect.notWithin (Absolute (Math.abs epsilon)) a b) (Expect.notWithin (Absolute (Math.abs epsilon)) b a)
            , fuzz2 niceFloat niceFloat "within absolute reflexive" <|
                \epsilon a ->
                    Expect.within (Absolute (Math.abs epsilon)) a a
            , fuzz3 niceFloat niceFloat niceFloat "within relative commutativity" <|
                \epsilon a b ->
                    same (Expect.within (Relative (Math.abs epsilon)) a b) (Expect.within (Relative (Math.abs epsilon)) b a)
            , fuzz3 niceFloat niceFloat niceFloat "notWithin relative commutativity" <|
                \epsilon a b ->
                    same (Expect.notWithin (Relative (Math.abs epsilon)) a b) (Expect.notWithin (Relative (Math.abs epsilon)) b a)
            , fuzz2 niceFloat niceFloat "within relative reflexive" <|
                \epsilon a ->
                    Expect.within (Relative (Math.abs epsilon)) a a
            ]
        ]
